apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../release.gradle'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.r0adkll.hardwired"

        minSdkVersion 17
        targetSdkVersion 23

        versionCode project.ext.versionCode
        versionName project.ext.versionName

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        alpha {
            versionName "alpha-v${project.ext.versionName}.${project.ext.versionCode}"
        }

        mock {
            versionName "mock-v${project.ext.versionName}.${project.ext.versionCode}"
        }

        prod{}
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "Hardwired-${variant.versionName}.apk")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    /*
     * Testing Suite
     */
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.0"

    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'

    compile 'com.52inc:52Kit-core:0.4.2'

    compile 'com.michaelpardo:ollie:0.3.2-SNAPSHOT'
    apt 'com.michaelpardo:ollie-compiler:0.3.2-SNAPSHOT'

    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'

    compile 'com.vikingsen:pocketknife-core:3.1.0'
    apt 'com.vikingsen:pocketknife-compiler:3.1.0'

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'

    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.0.0'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.hannesdorfmann:adapterdelegates:1.1.0'

}
